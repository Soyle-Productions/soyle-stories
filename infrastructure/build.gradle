import org.openjfx.gradle.JavaFXModule
import org.openjfx.gradle.JavaFXPlatform
import org.openjfx.gradle.JavaFXOptions
import org.apache.tools.ant.filters.*

plugins {
    id 'java'
    id 'application'
    id 'org.jetbrains.kotlin.jvm'
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'org.beryx.runtime' version '1.8.0'
}

apply from: "$rootDir/kotlin-module.gradle"
apply from: "$rootDir/local-repositories.gradle"

repositories {
    jcenter()
}

javafx {
    version = "14"
    modules = ['javafx.controls', 'javafx.fxml'/*, 'javafx.web', 'javafx.swing'*/]
}

def javaFXOptions = convention.findByType(JavaFXOptions.class) ?: convention.findPlugin(JavaFXOptions.class) ?: convention.getByType(JavaFXOptions.class)

dependencies {
    implementation libraries.soyle_stories_core

    implementation project(":application")
    implementation project(":data")
    implementation project(":gui")
    implementation "no.tornado:tornadofx:1.7.19", {
        exclude group: 'org.jetbrains.kotlin'
    }
    implementation libraries.coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-javafx:1.3.3"

    implementation "de.jensd:fontawesomefx-commons:11.0"
    implementation "de.jensd:fontawesomefx-controls:11.0"
    implementation "de.jensd:fontawesomefx-fontawesome:4.7.0-11"
    implementation "de.jensd:fontawesomefx-materialicons:2.2.0-11"
    implementation "de.jensd:fontawesomefx-emojione:2.2.7-11"

    testImplementation 'org.junit.vintage:junit-vintage-engine:5.6.0'
    testImplementation 'info.cukes:cucumber-java:1.0.11'
    testImplementation 'info.cukes:cucumber-jvm:1.0.11'
    testImplementation 'io.cucumber:cucumber-java8:5.5.0'
    testImplementation 'io.cucumber:cucumber-junit:5.5.0'
    testImplementation libraries.assertj

    testImplementation group: 'org.seleniumhq.selenium', name: 'selenium-chrome-driver', version: '3.11.0'
    testImplementation 'io.github.prashant-ramcharan:courgette-jvm:4.6.0'
    testImplementation 'io.github.prashant-ramcharan:webdriver-binary-downloader:1.2.2'

    testImplementation "org.testfx:testfx-core:4.0.16-alpha"
    testImplementation "org.testfx:testfx-junit5:4.0.16-alpha"
    testImplementation 'org.testfx:openjfx-monocle:jdk-12.0.1+2'

    testImplementation libraries.reflectionKt

    JavaFXPlatform.values().each { platform ->
        def cfg = configurations.create("javafx_" + platform.classifier)
        JavaFXModule.getJavaFXModules(javaFXOptions.modules).each { m ->
            project.getDependencies().add(cfg.name,
                    String.format("org.openjfx:%s:%s:%s", m.getArtifactName(), javaFXOptions.version, platform.classifier))
        }
    }
}

runtime {
    options.set(["--strip-debug", "--compress", "2", "--no-header-files", "--no-man-pages"])
    modules.set(['java.desktop',
                 'java.logging',
                 'java.prefs',
                 'java.xml',
                 'jdk.unsupported',
                 'java.scripting',
                 'jdk.jfr'])

    targetPlatform("linux", System.getenv("JAVA_HOME"))
    targetPlatform("mac", System.getenv("JAVA_HOME"))
    targetPlatform("win", System.getenv("JAVA_HOME"))
}

application {
    mainClassName = 'com.soyle.stories.soylestories.SoyleStoriesKt'
}

processResources {
    filesMatching("**/*.properties") {
        filter(ReplaceTokens, tokens: ["application.version": project.parent.property("version")], beginToken: '@', endToken: '@')
    }
}

tasks.withType(CreateStartScripts.class).each {script ->
    script.doFirst {
        script.classpath =  files("lib/*")
    }
}

tasks["runtime"].doLast {
    JavaFXPlatform.values().each { platform ->
        def cfg = configurations["javafx_" + platform.classifier]
        cfg.resolvedConfiguration.files.forEach { f ->
            copy {
                from(f)
                into("build/image/soyle-stories-${platform.classifier}/lib")
            }
        }
    }
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

task cucumber() {
    dependsOn assemble, compileTestJava
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--tags', '@scene and not @excluded', '--glue', 'com.soyle.stories', 'src/test/resources/features', '--strict']
        }
    }
}

tasks.withType(Test) {
    systemProperties.remove("java.endorsed.dirs")
}

task runFeaturesUsingJUnit(type: Test) {
    group = "UAT"

    include '**/suites/junit/FeatureSuite.class'
    outputs.upToDateWhen { false }
}
task runStoryEventFeatures() {
    group = "UAT"

    dependsOn assemble, compileTestJava
    doLast {
       javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', 'com.soyle.stories', '--tags', '@storyevent and not @excluded', 'src/test/resources/features', '--strict']
        }
    }
}